&НаКлиенте
процедура ОбновитьПредставлениеПериода()
	Если ВидОтчета = 0 Тогда
		НачалоПериода = НачалоКвартала(ДатаОкончанияОтчетногоПериода);
		ОкончаниеПериода = КонецКвартала(ДатаОкончанияОтчетногоПериода); 	
		Элементы.ДекорацияПериод.Заголовок =	ПредставлениеПериода(НачалоПериода, ОкончаниеПериода);
		
	КонецЕсли;
	
	Элементы.УменьшитьПериод.Видимость = ВидОтчета = 0;
	Элементы.УвеличитьПериод.Видимость = ВидОтчета = 0;
	Элементы.ДекорацияПериод.Видимость = ВидОтчета = 0;
	Элементы.ВыводитьТолькоКПКСОтчетностью.Видимость = ВидОтчета = 0;
	
	
	Элементы.ПериодОтчета.Видимость = ВидОтчета <> 0;
	Элементы.СписокКПК.Видимость = ВидОтчета <> 0;    
		
КонецПроцедуры   


&НаКлиенте
Процедура ПриИзмененииОтчетногоПериода(Режим)
	
	Знак = ?(Режим = "Увеличить", 1, -1);   	
	КоличествоМесяцев = 3;				 
	ДатаОкончанияОтчетногоПериода =  НачалоДня(КонецКвартала(ДобавитьМесяц(ДатаОкончанияОтчетногоПериода, КоличествоМесяцев * Знак)));			
	ОбновитьПредставлениеПериода();		
КонецПроцедуры

&НаКлиенте
Процедура УменьшитьПериод(Команда)
	ПриИзмененииОтчетногоПериода("Уменьшить");     
КонецПроцедуры

&НаКлиенте
Процедура УвеличитьПериод(Команда)
	ПриИзмененииОтчетногоПериода("Увеличить");    
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если НЕ ЗначениеЗаполнено(ДатаОкончанияОтчетногоПериода) Тогда
		ДатаОкончанияОтчетногоПериода =  КонецКвартала(ДобавитьМесяц(ТекущаяДата(), -3));
	КонецЕсли;
	
	ОбновитьПредставлениеПериода();
	
	Если ВидОтбора_КПК_УправлениеГУ = "" Тогда
		ВидОтбора_КПК_УправлениеГУ = "КПК";		
	КонецЕсли;
	
	ВидОтбора_КПК_УправлениеГУПриИзменении("");    

КонецПроцедуры

&НаСервере  
Процедура СформироватьНаСервере()
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	ЭтотОбъект.Результат.Очистить();
	
	МассивКПК = СписокКПК.ВыгрузитьЗначения();	 	
	МассивУправленийГУ = СписокУправленийГУ.ВыгрузитьЗначения();	 

	
	Если ВидОтчета = 0 Тогда
		ДатаНачалаОтчета    = НачалоКвартала(ДатаОкончанияОтчетногоПериода);
		ДатаОкончанияОтчета = ДатаОкончанияОтчетногоПериода;
	Иначе
		ДатаНачалаОтчета  = ПериодОтчета.ДатаНачала;
		ДатаОкончанияОтчета = КонецМесяца(ПериодОтчета.ДатаОкончания);     	
	КонецЕсли;
		
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ДатаНачалаОтчета", ДатаНачалаОтчета);
	СтруктураПараметров.Вставить("ДатаОкончанияОтчета", ДатаОкончанияОтчета);
	СтруктураПараметров.Вставить("МассивКПК", МассивКПК);
	СтруктураПараметров.Вставить("МассивУправленийГУ", МассивУправленийГУ);
	СтруктураПараметров.Вставить("ВидОтбора_КПК_УправлениеГУ", ВидОтбора_КПК_УправлениеГУ);
	СтруктураПараметров.Вставить("ВыводитьТолькоКПКСОтчетностью", ВыводитьТолькоКПКСОтчетностью);
	СтруктураПараметров.Вставить("ВидОтчета", ВидОтчета);

	МассивТабДокументов = Отчеты.СРО_СводныйОтчетПоКПК.СформироватьТабличныеДокументы(СтруктураПараметров);
	
	Результат.Вывести(МассивТабДокументов[0]);
	//Возврат МассивТабДокументов;	
	
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда)       	

	СформироватьНаСервере();

	//ТабличныйДокумент = СформироватьНаСервере();
	//Результат.Вывести(ТабличныйДокумент);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидОтчетаПриИзменении(Элемент)
	ОбновитьПредставлениеПериода(); 
	ВидОтбора_КПК_УправлениеГУПриИзменении("");

КонецПроцедуры

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	Если ТипЗнч(Расшифровка) = Тип("ДокументСсылка.ОтчетностьКПК") ИЛИ ТипЗнч(Расшифровка) = Тип("СправочникСсылка.Контрагенты") Тогда
		СтандартнаяОбработка = Ложь;    
		ОбработкаРасшифровки(Расшифровка);   
	КонецЕсли;  	

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаРасшифровки(ДокументОтчетности)
	СписокДействий = Новый СписокЗначений;
	СписокДействий.Добавить("ОткрытьСправочник", "Открыть справочник КПК");
	Если ТипЗнч(ДокументОтчетности) = Тип("ДокументСсылка.ОтчетностьКПК") Тогда
		СписокДействий.Добавить("ОткрытьДокументОтчетности", "Открыть документ отчетности");
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура("ДокументОтчетности", ДокументОтчетности);
	
	ОповещениеПослеВыбораДействиярасшифровки = Новый ОписаниеОповещения("ПослеВыбораДействияРасшифровки", ЭтаФорма, ДополнительныеПараметры);		
	СписокДействий.ПоказатьВыборЭлемента(ОповещениеПослеВыбораДействиярасшифровки, "", СписокДействий[0]);  	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораДействияРасшифровки(Элемент, Параметры) Экспорт

	Если Элемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Элемент.Значение = "ОткрытьСправочник" Тогда
		
		Если ТипЗнч(Параметры.ДокументОтчетности) = Тип("ДокументСсылка.ОтчетностьКПК") Тогда
			Пар = Новый Структура("Ключ", ПолучитьКПКДокумента(Параметры.ДокументОтчетности));
		Иначе
			Пар = Новый Структура("Ключ", Параметры.ДокументОтчетности);
		КонецЕсли;
			
		ОткрытьФорму("Справочник.Контрагенты.Форма.ФормаЭлемента", Пар);
	ИначеЕсли Элемент.Значение = "ОткрытьДокументОтчетности" Тогда
		Пар = Новый Структура("Ключ", Параметры.ДокументОтчетности);
		ОткрытьФорму("Документ.ОтчетностьКПК.Форма.ФормаДокумента", Пар);   			
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьКПКДокумента(ВыбДокумент)
	Если ТипЗнч(ВыбДокумент) = Тип("ДокументСсылка.ОтчетностьКПК") 
		ИЛИ ТипЗнч(ВыбДокумент) = Тип("ДокументОбъект.ОтчетностьКПК") Тогда
		Возврат ВыбДокумент.КПК;
	Иначе
		Возврат Неопределено;		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ВидОтбора_КПК_УправлениеГУПриИзменении(Элемент)
	Если ВидОтчета = 0 Тогда
		Элементы.СписокУправленийГУ.Видимость = Ложь;		
		Элементы.СписокКПК.Видимость = Ложь;
		Элементы.ВидОтбора_КПК_УправлениеГУ.Видимость = Ложь;
	Иначе	
		Элементы.СписокУправленийГУ.Видимость = НЕ ВидОтбора_КПК_УправлениеГУ = "КПК";		
		Элементы.СписокКПК.Видимость = ВидОтбора_КПК_УправлениеГУ = "КПК";		
		Элементы.ВидОтбора_КПК_УправлениеГУ.Видимость = Истина;

	КонецЕсли;

КонецПроцедуры



