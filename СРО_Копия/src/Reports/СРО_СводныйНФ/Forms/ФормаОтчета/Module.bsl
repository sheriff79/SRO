
&НаСервереБезКонтекста

Функция ПолучитьМассивПериодов(НачалоПериода, ОкончаниеПериода)
	
	МассивПериодов = Новый Массив;            	
	ВремПериод = КонецКвартала(НачалоПериода);    
	
	Пока ВремПериод <= ОкончаниеПериода Цикл
		МассивПериодов.Добавить(ВремПериод);  		
		ВремПериод = КонецКвартала(ДобавитьМесяц(ВремПериод, 3));
	КонецЦикла;
	
	Возврат МассивПериодов;
	
КонецФункции


&НаСервереБезКонтекста
Функция ПолучитьСтруктуруПараметров(ДокументОтчетности, МассивКодов)
	
	СтруктураЗначение = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "	
	|ВЫБРАТЬ
	|	ОтчетностьКПКТаблицаПоказателей.Ссылка КАК Ссылка,
	|	ОтчетностьКПКТаблицаПоказателей.Раздел КАК Раздел,
	|	ОтчетностьКПКТаблицаПоказателей.Показатель1 КАК Показатель
	|ИЗ
	|	Документ.ОтчетностьКПК.ТаблицаПоказателей КАК ОтчетностьКПКТаблицаПоказателей
	|ГДЕ
	|	ОтчетностьКПКТаблицаПоказателей.Ссылка = &ДокументОтчетности
	|	И ОтчетностьКПКТаблицаПоказателей.Раздел.Код В (&МассивКодов)";
	
	Запрос.УстановитьПараметр("ДокументОтчетности", ДокументОтчетности);
	Запрос.УстановитьПараметр("МассивКодов", МассивКодов);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл                                                                    		
		СтруктураЗначение.Вставить("Раздел" + СокрЛП(Выборка.Раздел.Код), Выборка.Показатель);	
	КонецЦикла;
	
	Возврат СтруктураЗначение;                                                                       
	
КонецФункции


&НаСервере
Функция СформироватьНаСервере() 	
	
	ТабличныйДокумент = Новый ТабличныйДокумент; 
	ОтчетОбъект = РеквизитФормыВЗначение("Отчет"); 	 
	
	ВремТаблицаДляРассылки = РеквизитФормыВЗначение("ТаблицаДляРассылки");
	
	НайденныеСтроки = ВремТаблицаДляРассылки.найтиСтроки(Новый Структура("Флаг", Истина));
	
	МассивКПК = Новый Массив;
	Для Каждого НайденнаяСтрока ИЗ НайденныеСтроки Цикл   		
		МассивКПК.Добавить(НайденнаяСтрока.КПК);	
	КонецЦикла;
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ДатаНачалаОтчета", ПериодОтчета.ДатаНачала);
	СтруктураПараметров.Вставить("ДатаОкончанияОтчета", КонецДня(ПериодОтчета.ДатаОкончания));
	СтруктураПараметров.Вставить("МассивКПК", МассивКПК); 	                                       	
	СтруктураПараметров.Вставить("МассивСубъектов", ОтборПоСубъектам.ВыгрузитьЗначения()); 	
	СтруктураПараметров.Вставить("МассивУправленийГУ", ОтборПоУправлениямГУ.ВыгрузитьЗначения()); 	
	СтруктураПараметров.Вставить("ВидОтбораОтчета", ВидОтбораОтчета); 
	
	МассивТабДокументов = Отчеты.СРО_СводныйНФ.СформироватьТабличныеДокументы(СтруктураПараметров);	
	
	Возврат МассивТабДокументов[0];	

КонецФункции 


&НаКлиенте
Процедура Сформировать(Команда)  
	
	ТабличныйДокумент = СформироватьНаСервере();  
	ТабличныйДокумент.Показать("Сводный ФН"); 	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)      
	ОбновитьВидимостьРеквизитов();
КонецПроцедуры                    

&НаСервере
Функция ПолучитьКПКДокумента(ВыбДокумент)
	Если ТипЗнч(ВыбДокумент) = Тип("ДокументСсылка.ОтчетностьКПК") 
		ИЛИ ТипЗнч(ВыбДокумент) = Тип("ДокументОбъект.ОтчетностьКПК") Тогда
		Возврат ВыбДокумент.КПК;
	Иначе
		Возврат Неопределено;		
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ПолучитьТаблицуОшибокНаСервере() 	
	
	ВремТЗ = Отчет.ТаблицаОшибок.Выгрузить();                                          				   
	
	ВремТЗ.Свернуть("КПК, НазваниеТаблицы, ОписаниеУсловия, НомерВРеестреСРО, КодОшибки", "КоличествоОшибок, ПорядокСортировки");
	
	ПорядокСортировки = 0;
	Для Каждого ТекСтрока ИЗ ВремТЗ Цикл
		
		ПорядокСортировки = ПорядокСортировки + 1;      		
		ТекСтрока.ПорядокСортировки = ПорядокСортировки;		
		
	КонецЦикла;

	Возврат ВремТЗ;	
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьСписокКПК(Команда)
	
	ТаблицаДляРассылки.Очистить();
	
	ДопПараметры = Новый Структура;
	ОткрытьФорму("Отчет.СРО_СводныйНФ.Форма.ФормаОтбораПоСписку", ДопПараметры, ЭтаФорма);   

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицу(МассивКПК)
	
	ВремТаблица = РеквизитФормыВЗначение("ТаблицаДляРассылки");
	
	Для Каждого ТекКПК ИЗ МассивКПК Цикл		
		НоваяСтрока = ВремТаблица.Добавить();		
		НоваяСтрока.КПК = ТекКПК;
		НоваяСтрока.НомерВРеестреСРО = СокрЛП(ТекКПК.НомерВРеестреСРО);
		НоваяСтрока.ИНН = СокрЛП(ТекКПК.ИНН);
		НоваяСтрока.ОГРН = СокрЛП(ТекКПК.ОГРН);
		НоваяСтрока.Флаг = Истина;		
		
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ВремТаблица, "ТаблицаДляРассылки");
	
КонецПроцедуры


&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)	
	ЗаполнитьТаблицу(ВыбранноеЗначение);
КонецПроцедуры

&НаСервере
Функция ПолучитьДанныеКПК(КПК)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Контрагенты.ИНН КАК ИНН,
	|	Контрагенты.ОГРН КАК ОГРН,
	|	Контрагенты.НомерВРеестреСРО КАК НомерВРеестреСРО
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.Ссылка = &КПК";
	
	Запрос.УстановитьПараметр("КПК", КПК);
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДанныхКПК = Новый Структура("ИНН, ОГРН, НомерВРеестреСРО");
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат СтруктураДанныхКПК;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	ЗаполнитьЗначенияСвойств(СтруктураДанныхКПК, Выборка);
	
	Возврат СтруктураДанныхКПК;
	
КонецФункции
	
&НаКлиенте
Процедура ТаблицаДляРассылкиКПКПриИзменении(Элемент)
	
	СтруктураДанныхКПК =ПолучитьДанныеКПК(Элементы.ТаблицаДляРассылки.ТекущиеДанные.КПК);
 
	Элементы.ТаблицаДляРассылки.ТекущиеДанные.ИНН = СтруктураДанныхКПК.ИНН;
	Элементы.ТаблицаДляРассылки.ТекущиеДанные.ОГРН = СтруктураДанныхКПК.ОГРН;
	Элементы.ТаблицаДляРассылки.ТекущиеДанные.НомерВРеестреСРО = СтруктураДанныхКПК.НомерВРеестреСРО;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВидимостьРеквизитов()    		
	Элементы.ОтборПоСубъектам.Видимость = ВидОтбораОтчета = 1;
	Элементы.ОтборПоУправлениямГУ.Видимость = ВидОтбораОтчета = 2;
	Элементы.ТаблицаДляРассылки.Видимость = ВидОтбораОтчета = 0; 	
КонецПроцедуры

&НаКлиенте
Процедура ВидОтбораОтчетаПриИзменении(Элемент)
	ОбновитьВидимостьРеквизитов();		
КонецПроцедуры


